<html><head><link rel=stylesheet type="text/css" href="../libref.css"><title>rules.t</title></head><body>
<table class=ban><tr><td><h1>rules.t</h1><td align=right><a href="../file/rules.t.html">documentation</a></table><pre>
<a name="1"></a>#charset "us-ascii"
<a name="2"></a>#include "advlite.h"
<a name="3"></a>
<a name="4"></a>/* 
<a name="5"></a> *   RULES EXTENSION by Eric Eve July 2014
<a name="6"></a> *
<a name="7"></a> *   The rules.t extension allows Inform7-style rules and rulebooks to be
<a name="8"></a> *   defined in a TADS 3 game.
<a name="9"></a> */
<a name="10"></a>
<a name="11"></a>
<a name="12"></a>
<a name="13"></a>
<a name="14"></a>/* 
<a name="15"></a> *   A RuleBook is a container for one or more rules. Calling the follow()
<a name="16"></a> *   method of a RuleBook causes each of its contained rules to be executed in
<a name="17"></a> *   turn until one returns a non-null value. That value is then returned to the
<a name="18"></a> *   caller of the RuleBook. [DEFINED IN RULES EXTENSION]
<a name="19"></a> */
<a name="20"></a>     
<a name="21"></a>class RuleBook: PreinitObject    
<a name="22"></a>    
<a name="23"></a>    /* A list of rules contained in this rulebook */
<a name="24"></a>    contents = []
<a name="25"></a>    
<a name="26"></a>    /* 
<a name="27"></a>     *   The actor to use for comparison with the who property for rules in this
<a name="28"></a>     *   RuleBook. The default is gPlayerChar, but for some RuleBooks gActor may
<a name="29"></a>     *   be more appropriate.
<a name="30"></a>     */
<a name="31"></a>    actor = gPlayerChar
<a name="32"></a>    
<a name="33"></a>    /* Add a rule to the contents of this rulebook */
<a name="34"></a>    addToContents(ru)
<a name="35"></a>    {
<a name="36"></a>        contents += ru;
<a name="37"></a>    }
<a name="38"></a>    
<a name="39"></a>    /* Remove a rule from the contents of this rulebook */
<a name="40"></a>    removeFromContents(ru)
<a name="41"></a>    {
<a name="42"></a>        contents -= ru;
<a name="43"></a>    }
<a name="44"></a>    
<a name="45"></a>    
<a name="46"></a>    /* 
<a name="47"></a>     *   Follow this Rule. This is the method game code will normally call to
<a name="48"></a>     *   make use of this RuleBook. Each of our rules will be tested to see if
<a name="49"></a>     *   it matches its conditions; we then run through those of our rules that
<a name="50"></a>     *   match their rules in order of precedence until one returns a non-null
<a name="51"></a>     *   value, which we then in turn return to our caller. If no rule returns a
<a name="52"></a>     *   non-null value we return our own default value, which is normally nil.
<a name="53"></a>     *
<a name="54"></a>     *   This method can be called with as many arguments as the game code finds
<a name="55"></a>     *   useful, or with none at all. Our arguments will then be passed on to
<a name="56"></a>     *   each Rule that is called. The first argument will also be stored in our
<a name="57"></a>     *   matchObj property, which our Rules can compare with their own matchObj
<a name="58"></a>     *   condition to see if they match. This allows game code to, for example,
<a name="59"></a>     *   run a RuleBook related to some object that isn't one of the objects
<a name="60"></a>     *   directly involved in the current action.
<a name="61"></a>     */
<a name="62"></a>    follow([args])
<a name="63"></a>    {
<a name="64"></a>        /* 
<a name="65"></a>         *   If we have any arguments at all, store the first one in our
<a name="66"></a>         *   matchObj property for comparison with our Rules' matchObj
<a name="67"></a>         *   conditions.
<a name="68"></a>         */
<a name="69"></a>        if(args.length &gt; 0)
<a name="70"></a>            matchObj = args[1];
<a name="71"></a>        
<a name="72"></a>        /* Carry out any custom initialization required. */
<a name="73"></a>        initBook(args...);
<a name="74"></a>        
<a name="75"></a>        /* 
<a name="76"></a>         *   Make sure that all the Rules in our contents list know that we're
<a name="77"></a>         *   the RuleBook that's currently calling them. This is necessary in
<a name="78"></a>         *   case the same Rule is associated with more than one RuleBook.
<a name="79"></a>         */
<a name="80"></a>        contents.forEach({ r: r.setRulebook(self) });
<a name="81"></a>        
<a name="82"></a>        /* Extract the subset of Rules that match their conditions. */
<a name="83"></a>        local validRules = contents.subset({r: r.matchConditions});
<a name="84"></a>        
<a name="85"></a>        /* Sort the matching rules in descending order of precedence. */
<a name="86"></a>        validRules = validRules.sort(SortDesc, {a, b: a.compareTo(b)} );
<a name="87"></a>        
<a name="88"></a>        /* 
<a name="89"></a>         *   Go through each of the matching rules in turn, calling its follow
<a name="90"></a>         *   method. If any rule returns a non-null value, stop running through
<a name="91"></a>         *   the rules and return that value to our caller.
<a name="92"></a>         */
<a name="93"></a>        foreach(local ru in validRules)
<a name="94"></a>        {
<a name="95"></a>            IfDebug(rules, 
<a name="96"></a>                    "[Executing rule: &lt;&lt;valToSym(ru)&gt;&gt; in rulebook
<a name="97"></a>                    &lt;&lt;valToSym(self)&gt;&gt;]\n" );
<a name="98"></a>            local res = ru.follow(args...);
<a name="99"></a>            
<a name="100"></a>            if(res != contValue)
<a name="101"></a>                return res;
<a name="102"></a>        }
<a name="103"></a>        
<a name="104"></a>        /* 
<a name="105"></a>         *   If no rule returned a non-null value, return our own defaultVal to
<a name="106"></a>         *   our caller.
<a name="107"></a>         */
<a name="108"></a>        return defaultVal;
<a name="109"></a>    }
<a name="110"></a>    
<a name="111"></a>    
<a name="112"></a>    /* 
<a name="113"></a>     *   contValue (continue value) is the value that a Rule in this RuleBook
<a name="114"></a>     *   needs to return to avoid the RuleBook stopping at that Rule (rather
<a name="115"></a>     *   than going on to consider more Rules). By default this is null, which
<a name="116"></a>     *   means by default a Rule that does not explicitly return a value (and so
<a name="117"></a>     *   effectivelt returns nil) will stop the RuleBook. If you want the
<a name="118"></a>     *   default behaviour for this RuleBook to be not for Rules to stop the
<a name="119"></a>     *   book, then override this to nil.
<a name="120"></a>     */
<a name="121"></a>    contValue = null
<a name="122"></a>    
<a name="123"></a>    /*  
<a name="124"></a>     *   The default value to return to our caller. By default this is the same
<a name="125"></a>     *   as our contValue , to make it easy to test whether we any rule returned
<a name="126"></a>     *   a non-null value. By default a rule that does something will return
<a name="127"></a>     *   nil, so if no rule does anything we want to return a different value.
<a name="128"></a>     *   By making the defaultValue the same as the contValue, we ensure that we
<a name="129"></a>     *   can tell our caller that no rule was executed (if that is indeed the
<a name="130"></a>     *   case).
<a name="131"></a>     */
<a name="132"></a>    defaultVal = contValue
<a name="133"></a>    
<a name="134"></a>    /*   
<a name="135"></a>     *   The value our associated rules use by default to stop this RuleBook
<a name="136"></a>     *   considering any further rules (when a Rule uses the stop macro). By
<a name="137"></a>     *   default we use a value of true.
<a name="138"></a>     */
<a name="139"></a>    
<a name="140"></a>    stopValue = true
<a name="141"></a>    
<a name="142"></a>    /* 
<a name="143"></a>     *   Game code can use this method to initialize the values of custom
<a name="144"></a>     *   RuleBook properties at the start of the processing of following a
<a name="145"></a>     *   RuleBook.
<a name="146"></a>     */
<a name="147"></a>    initBook([args]) { }
<a name="148"></a>    
<a name="149"></a>    /*   
<a name="150"></a>     *   The object (or any other value) to be matched by our Rule's matchObj
<a name="151"></a>     *   conditions if they have any. This property is set by our follow()
<a name="152"></a>     *   method (from its first argument) and so should not normally be directly
<a name="153"></a>     *   changed from game code.
<a name="154"></a>     */
<a name="155"></a>    matchObj = nil
<a name="156"></a>    
<a name="157"></a>;
<a name="158"></a>
<a name="159"></a>/* Preinitializer for Rules. [DEFINED IN RULES EXTENSION]*/
<a name="160"></a>rulePreinit:PreinitObject
<a name="161"></a>    execute()
<a name="162"></a>    {
<a name="163"></a>        /* Initialize all the Rules in the game. */
<a name="164"></a>        for(local ru = firstObj(Rule); ru != nil; ru = nextObj(ru, Rule))
<a name="165"></a>        {
<a name="166"></a>            ru.initializeRule();
<a name="167"></a>        }       
<a name="168"></a>    }
<a name="169"></a>;
<a name="170"></a>
<a name="171"></a>
<a name="172"></a>/* 
<a name="173"></a> *   A Rule is an object that defines a set of conditions that need to be met
<a name="174"></a> *   for it to be executed when its RuleBook is run and a method that's executed
<a name="175"></a> *   when its conditions are met. A Rule can be associated with one or more
<a name="176"></a> *   RuleBooks; it starts out in the RuleBook with which it is associated via
<a name="177"></a> *   its + property (i.e. its location). [DEFINED IN RULES EXTENSION]
<a name="178"></a> */
<a name="179"></a>class Rule: object
<a name="180"></a>    /* 
<a name="181"></a>     *   Our location is the RuleBook with which we start out being associated.
<a name="182"></a>     *   Normally this will be defined by locating a Rule inside its RukeBook
<a name="183"></a>     *   using the + notation.
<a name="184"></a>     */
<a name="185"></a>    location = nil
<a name="186"></a>    
<a name="187"></a>    /*   
<a name="188"></a>     *   The rulebook that's currently considering us. Normally this will be our
<a name="189"></a>     *   location, but it could be a different RuleBook if we belong to one.
<a name="190"></a>     *   Note that this property is automatically set by the library and so it
<a name="191"></a>     *   should never need to be altered by game code.
<a name="192"></a>     */
<a name="193"></a>    rulebook = location
<a name="194"></a>      
<a name="195"></a>    /*   
<a name="196"></a>     *   Set our current rulebook to r. Note that this method is normally called
<a name="197"></a>     *   by the Rulebook that's running us, and shouldn't normally be used by
<a name="198"></a>     *   game code.
<a name="199"></a>     */
<a name="200"></a>    setRulebook(r) { rulebook = r; }
<a name="201"></a>    
<a name="202"></a>    /*   A list of all the rulebooks this rule is currently associated with. */
<a name="203"></a>    rulebooks = []    
<a name="204"></a>    
<a name="205"></a>    /*   
<a name="206"></a>     *   Initialize this Rule by adding it to the contents list of its location
<a name="207"></a>     *   and calculating its specificity (i.e. how specific its conditions are)
<a name="208"></a>     */
<a name="209"></a>    initializeRule()
<a name="210"></a>    {
<a name="211"></a>        if(location)
<a name="212"></a>        {
<a name="213"></a>            location.addToContents(self);               
<a name="214"></a>            rulebooks += location;
<a name="215"></a>        }
<a name="216"></a>        
<a name="217"></a>        specificity = calcSpecficity();
<a name="218"></a>    }
<a name="219"></a>    
<a name="220"></a>    /* 
<a name="221"></a>     *   Do whatever this Rule needs to do when its conditions are met. This
<a name="222"></a>     *   method will need to be defined on eacg individual Rule in game code.
<a name="223"></a>     */
<a name="224"></a>         
<a name="225"></a>    follow([args])
<a name="226"></a>    {
<a name="227"></a>    }
<a name="228"></a>    
<a name="229"></a>    /* 
<a name="230"></a>     *   The priority of this Rule. This can be used to alter the order in which
<a name="231"></a>     *   this Rule is considered in its RuleBook. If two Rules have different
<a name="232"></a>     *   priorities they will be run in priority order, highest priority first.
<a name="233"></a>     *   The default value is 100.
<a name="234"></a>     */ 
<a name="235"></a>    priority = 100
<a name="236"></a>    
<a name="237"></a>    /*   
<a name="238"></a>     *   Where two Rules have the same priority, the one with the more specific
<a name="239"></a>     *   conditions is taken first. The specificity property holds a measure of
<a name="240"></a>     *   the Rule's specificity which is calculated by the calcSpecificity()
<a name="241"></a>     *   method at PreInit.
<a name="242"></a>     */
<a name="243"></a>    specificity = nil
<a name="244"></a>    
<a name="245"></a>    /* 
<a name="246"></a>     *   Return true if this Rule should always execute after other (despite all
<a name="247"></a>     *   other ranking criteria). By default we return true if and only if other
<a name="248"></a>     *   is in our execAfter list.
<a name="249"></a>     */   
<a name="250"></a>    runAfter(other)
<a name="251"></a>    {
<a name="252"></a>        return valToList(execAfter).indexOf(other) != nil;        
<a name="253"></a>    }
<a name="254"></a>    
<a name="255"></a>    /* 
<a name="256"></a>     *   A list of Rules this Rule should specifically run after; this overrides
<a name="257"></a>     *   all other ranking.
<a name="258"></a>     */
<a name="259"></a>    execAfter = []
<a name="260"></a>    
<a name="261"></a>    /* 
<a name="262"></a>     *   Return true if this Rule should always execute before other (despite all
<a name="263"></a>     *   other ranking criteria). By default we return true if and only if other
<a name="264"></a>     *   is in our execBefore list.
<a name="265"></a>     */
<a name="266"></a>    runBefore(other)
<a name="267"></a>    {
<a name="268"></a>        return valToList(execBefore).indexOf(other) != nil;
<a name="269"></a>    }
<a name="270"></a>    
<a name="271"></a>    /*  
<a name="272"></a>     *   A list of Rules this Rule should specifically run before; this
<a name="273"></a>     *   overrides all other ranking except for runAfter/execAfter.     
<a name="274"></a>     *
<a name="275"></a>     */
<a name="276"></a>    execBefore = []
<a name="277"></a>    
<a name="278"></a>    /*  
<a name="279"></a>     *   A Rule is normally active (that is it will normally be considered when
<a name="280"></a>     *   a RuleBook is being followed) but it can be temporarily disabled by
<a name="281"></a>     *   setting its isActive property to nil.
<a name="282"></a>     */
<a name="283"></a>    isActive = true
<a name="284"></a>
<a name="285"></a>    /* Make this Rule active */
<a name="286"></a>    activate() { isActive = true; }
<a name="287"></a>    
<a name="288"></a>    /* Make this Rule inactive */
<a name="289"></a>    deactivate() { isActive = nil; }
<a name="290"></a>    
<a name="291"></a>    /*
<a name="292"></a>     *   Calculate the specificity of this Rule. The principles are (a) Rules
<a name="293"></a>     *   that specify more conditions are more specific than Rule that specify
<a name="294"></a>     *   fewer condition; (b) conditions involving specific objects are more
<a name="295"></a>     *   specific that those relating to classes and (c) Rooms are more specific
<a name="296"></a>     *   than Regions in a where condition.
<a name="297"></a>     */
<a name="298"></a>    calcSpecficity()
<a name="299"></a>    {       
<a name="300"></a>        local p = 0;
<a name="301"></a>       
<a name="302"></a>        /* a 'when' has priority over no 'when' */
<a name="303"></a>        if(propDefined(&amp;when))
<a name="304"></a>            p += 10;
<a name="305"></a>        
<a name="306"></a>        /* 
<a name="307"></a>         *   a 'where' has priority over no 'where', and a where property that
<a name="308"></a>         *   specifies a Room is more specific than one that specifies a Region.
<a name="309"></a>         */
<a name="310"></a>        if(propDefined(&amp;where))
<a name="311"></a>            p += valToList(where).indexWhich({r: r.ofKind(Room)}) ? 10 : 5;
<a name="312"></a>        
<a name="313"></a>        /* a 'during' has priority over no 'during' */
<a name="314"></a>        if(propDefined(&amp;during))
<a name="315"></a>            p += 10;
<a name="316"></a>        
<a name="317"></a>        /* a 'who' has priority over no 'who' */
<a name="318"></a>        if(propDefined(&amp;who))
<a name="319"></a>            p += 10;
<a name="320"></a>        
<a name="321"></a>        /* an 'action' has prioity over no 'action' */
<a name="322"></a>        if(propDefined(&amp;action))
<a name="323"></a>        {
<a name="324"></a>            p += 5;
<a name="325"></a>        }
<a name="326"></a>        
<a name="327"></a>        /* 
<a name="328"></a>         *   A Rule that refers to command objects (direct, indirect and
<a name="329"></a>         *   accessory) is more specific than one that does not, and particular
<a name="330"></a>         *   objects are more specific than classes.
<a name="331"></a>         */
<a name="332"></a>        for(local prop in [&amp;dobj, &amp;iobj, &amp;aobj])
<a name="333"></a>        {
<a name="334"></a>            if(propDefined(prop))
<a name="335"></a>            {
<a name="336"></a>                p += valToList(self.(prop)).indexWhich( {x: !x.isClass()} ) !=
<a name="337"></a>                nil ? 2 : 1;
<a name="338"></a>            }                
<a name="339"></a>        }
<a name="340"></a>        
<a name="341"></a>        /* 
<a name="342"></a>         *   If matchObj is defined, then what we do with it depends on the type
<a name="343"></a>         *   of value(s) it contains.
<a name="344"></a>         */
<a name="345"></a>        if(propDefined(&amp;matchObj))
<a name="346"></a>        {
<a name="347"></a>            switch(propType(&amp;matchObj))
<a name="348"></a>            {
<a name="349"></a>                /* If matchObj is nil, then ignore it. */
<a name="350"></a>            case TypeNil:
<a name="351"></a>                break;
<a name="352"></a>                
<a name="353"></a>                /* 
<a name="354"></a>                 *   If it's an object, increase our specificity by 10, unless
<a name="355"></a>                 *   it's a class, in which case only increase it by 5 (a class
<a name="356"></a>                 *   is less specific than an object).
<a name="357"></a>                 */
<a name="358"></a>            case TypeObject:
<a name="359"></a>                p += (matchObj.isClass ? 5 : 10);
<a name="360"></a>                break;
<a name="361"></a>                /* 
<a name="362"></a>                 *   If it's a list, check what kind of values the list
<a name="363"></a>                 *   contains.
<a name="364"></a>                 */
<a name="365"></a>            case TypeList:
<a name="366"></a>                /*  
<a name="367"></a>                 *   If the first item in the list is an object, assume the
<a name="368"></a>                 *   whole list contains objects, then increase our specificity
<a name="369"></a>                 *   by 10 if any of those objects is not a class, and by 5
<a name="370"></a>                 *   otherwise (classes are less specific than objects).
<a name="371"></a>                 */
<a name="372"></a>                if(matchObj.length &gt; 0 &amp;&amp; dataType(matchObj[1]) == TypeObject)
<a name="373"></a>                {
<a name="374"></a>                    if(matchObj.indexWhich({o: !o.isClass}))
<a name="375"></a>                        p += 10;
<a name="376"></a>                    else
<a name="377"></a>                        p += 5;
<a name="378"></a>                }
<a name="379"></a>                /* 
<a name="380"></a>                 *   Otherwise, if it's some other kind of value, simply
<a name="381"></a>                 *   increase our specificity by 10.
<a name="382"></a>                 */
<a name="383"></a>                else
<a name="384"></a>                    p += 10;
<a name="385"></a>                
<a name="386"></a>                break;
<a name="387"></a>                /* 
<a name="388"></a>                 *   For any other kind of value, simply increase our
<a name="389"></a>                 *   specificity by 10.
<a name="390"></a>                 */
<a name="391"></a>            default:
<a name="392"></a>                p += 10;
<a name="393"></a>                break;
<a name="394"></a>                
<a name="395"></a>            }
<a name="396"></a>        }
<a name="397"></a>        
<a name="398"></a>        /* Check if our present property is defined. */
<a name="399"></a>        if(propDefined(&amp;present))
<a name="400"></a>        {
<a name="401"></a>            if(dataType(&amp;present) == TypeObject &amp;&amp; present.isClass)
<a name="402"></a>                p += 5;
<a name="403"></a>            else
<a name="404"></a>                p += 10;
<a name="405"></a>        }
<a name="406"></a>           
<a name="407"></a>        
<a name="408"></a>        
<a name="409"></a>        /* Return the result of the calculation. */
<a name="410"></a>        return p;       
<a name="411"></a>    }
<a name="412"></a>    
<a name="413"></a>    
<a name="414"></a>    /*
<a name="415"></a>     *   Get the processing priority sorting order relative to another
<a name="416"></a>     *   Rule. 
<a name="417"></a>     */
<a name="418"></a>    compareTo(other)
<a name="419"></a>    {
<a name="420"></a>        /*  
<a name="421"></a>         *   If we specifically want this Rule to run after other, rank us after
<a name="422"></a>         *   other
<a name="423"></a>         */
<a name="424"></a>        if(runAfter(other))           
<a name="425"></a>            return -1;        
<a name="426"></a>        
<a name="427"></a>        /*   
<a name="428"></a>         *   If we specifically want this Rule to run before other, rank us
<a name="429"></a>         *   before other.
<a name="430"></a>         */
<a name="431"></a>        if(runBefore(other))
<a name="432"></a>            return 1;        
<a name="433"></a>        
<a name="434"></a>        /* 
<a name="435"></a>         *   If the two Rules have different priorities, rank them in order of
<a name="436"></a>         *   priority.
<a name="437"></a>         */
<a name="438"></a>        if(priority != other.priority)   
<a name="439"></a>            return priority &gt; other.priority ? 1 : -1;
<a name="440"></a>        
<a name="441"></a>        /*  
<a name="442"></a>         *   Otherwise, if they have different speficifities, rank them in order
<a name="443"></a>         *   of specificity.
<a name="444"></a>         */
<a name="445"></a>        if(specificity &gt;= other.specificity)
<a name="446"></a>            return specificity &gt; other.specificity ? 1 : -1;
<a name="447"></a>        
<a name="448"></a>        /* 
<a name="449"></a>         *   Failing all else, go by the relative location of the source
<a name="450"></a>         *   code definitions: the definition that appears later in the
<a name="451"></a>         *   source code takes precedence.  If the two are defined in
<a name="452"></a>         *   different modules, the one in the later module takes
<a name="453"></a>         *   precedence.  
<a name="454"></a>         */
<a name="455"></a>        if (sourceTextGroup != other.sourceTextGroup)
<a name="456"></a>            return sourceTextGroup.sourceTextGroupOrder
<a name="457"></a>            - other.sourceTextGroup.sourceTextGroupOrder;
<a name="458"></a>
<a name="459"></a>        /* they're in the same module, so the later one takes precedence */
<a name="460"></a>        return sourceTextOrder - other.sourceTextOrder;
<a name="461"></a>        
<a name="462"></a>    }
<a name="463"></a>    
<a name="464"></a>    /* Check whether a Rule matches its where, when, who and during conditions. */    
<a name="465"></a>    matchConditions()    
<a name="466"></a>    {
<a name="467"></a>        /* If this Rule is currently inactive it can't match any conditions. */
<a name="468"></a>        if(!isActive)
<a name="469"></a>            return nil;
<a name="470"></a>        
<a name="471"></a>        /* first check the where condition, if there is one. */
<a name="472"></a>        if(propDefined(&amp;where))
<a name="473"></a>        {
<a name="474"></a>            local whereLst = valToList(where);
<a name="475"></a>                                    
<a name="476"></a>            /* 
<a name="477"></a>             *   if we can't match any item in the where list to the player
<a name="478"></a>             *   char's current location, we don't meet the where condition, so
<a name="479"></a>             *   return nil
<a name="480"></a>             */
<a name="481"></a>            if(whereLst.indexWhich( {loc: gActor.isIn(loc)}) == nil)
<a name="482"></a>                return nil;
<a name="483"></a>        }
<a name="484"></a>        
<a name="485"></a>        /* 
<a name="486"></a>         *   Interpret 'when' as simply a routine that returns true or nil
<a name="487"></a>         *   aocording to some condition defined by the author; so we simply
<a name="488"></a>         *   test whether when returns nil if the property is defined.
<a name="489"></a>         */        
<a name="490"></a>        if(propDefined(&amp;when) &amp;&amp; when() == nil)
<a name="491"></a>            return nil;       
<a name="492"></a>        
<a name="493"></a>         /* check the who condition, if there is one. */
<a name="494"></a>        if(propDefined(&amp;who))
<a name="495"></a>        {
<a name="496"></a>            local whoLst = valToList(who);
<a name="497"></a>                        
<a name="498"></a>            
<a name="499"></a>            /* 
<a name="500"></a>             *   If we can't match any item in the who list to the current
<a name="501"></a>             *   actor, we don't meet the who condition, so return nil
<a name="502"></a>             */
<a name="503"></a>            if(whoLst.indexOf(gActor) == nil)
<a name="504"></a>                return nil;
<a name="505"></a>        }
<a name="506"></a>        
<a name="507"></a>        
<a name="508"></a>        /* 
<a name="509"></a>         *   if we're using the scene manager and a during condition is
<a name="510"></a>         *   specified, test whether the scene is currently happening.
<a name="511"></a>         */        
<a name="512"></a>        if(defined(sceneManager) &amp;&amp; propDefined(&amp;during))
<a name="513"></a>        {
<a name="514"></a>            local duringList = valToList(during);
<a name="515"></a>            
<a name="516"></a>            if(duringList.indexWhich({s: s.isHappening}) == nil)
<a name="517"></a>                return nil;
<a name="518"></a>        }
<a name="519"></a>        
<a name="520"></a>        /* 
<a name="521"></a>         *   If we've specified an action to match, test whether gAction (the
<a name="522"></a>         *   current action) matches it.
<a name="523"></a>         */
<a name="524"></a>        if(propDefined(&amp;action) 
<a name="525"></a>           &amp;&amp; valToList(action).indexWhich({a: gAction.ofKind(a)}) == nil)
<a name="526"></a>            return nil;
<a name="527"></a>        
<a name="528"></a>        
<a name="529"></a>        /*  
<a name="530"></a>         *   If we've specified a dobj, iobj and/or aobj to match, test whether
<a name="531"></a>         *   they match the direct object, indirect object and/or accessory
<a name="532"></a>         *   object of the current action (provided there is one, which there
<a name="533"></a>         *   may not be at startup).
<a name="534"></a>         */
<a name="535"></a>        if(gAction)
<a name="536"></a>        {
<a name="537"></a>            for(local objs in [[&amp;dobj, gDobj], [&amp;iobj, gIobj], [&amp;aobj, gAobj]])
<a name="538"></a>            {
<a name="539"></a>                local prop = objs[1];
<a name="540"></a>                local obj = objs[2];
<a name="541"></a>                
<a name="542"></a>                if(propDefined(prop) 
<a name="543"></a>                   &amp;&amp; valToList(self.(prop)).indexWhich({o: obj.ofKind(o)}) == nil)
<a name="544"></a>                    return nil;          
<a name="545"></a>                
<a name="546"></a>            }
<a name="547"></a>        }
<a name="548"></a>        
<a name="549"></a>        /* 
<a name="550"></a>         *   If we have a matchObj defined, test whether it matches our current
<a name="551"></a>         *   rulebook's matchjObj.
<a name="552"></a>         */
<a name="553"></a>        if(propDefined(&amp;matchObj))
<a name="554"></a>        {
<a name="555"></a>            local mList = valToList(matchObj);
<a name="556"></a>            local mo = rulebook.matchObj;
<a name="557"></a>            
<a name="558"></a>            if(mList.length &gt; 0)
<a name="559"></a>            {
<a name="560"></a>                /* 
<a name="561"></a>                 *   If we want to match an object (or class), test whether the
<a name="562"></a>                 *   rulebook's match obj is of the appropriate kind. Since an
<a name="563"></a>                 *   object is always of its own kind, this tests either whether
<a name="564"></a>                 *   the rulebook's matchObj appears in our matchObj list or
<a name="565"></a>                 *   whether it belongs to one of the classes in our matchObh
<a name="566"></a>                 *   list.
<a name="567"></a>                 */
<a name="568"></a>                if(dataType(mList[1]) == TypeObject)
<a name="569"></a>                {
<a name="570"></a>                    if(mList.indexWhich({o: mo.ofKind(o) } ) == nil)
<a name="571"></a>                        return nil;
<a name="572"></a>                }
<a name="573"></a>                /* 
<a name="574"></a>                 *   Otherwise, if we're not testing for an object, simply test
<a name="575"></a>                 *   whether the value of our rulebook's matchObj is equal to
<a name="576"></a>                 *   anyhting in our matchObj property.
<a name="577"></a>                 */
<a name="578"></a>                else if(mList.indexOf(mo) == nil)
<a name="579"></a>                    return nil;               
<a name="580"></a>                    
<a name="581"></a>            }
<a name="582"></a>        }       
<a name="583"></a>        
<a name="584"></a>        /* 
<a name="585"></a>         *   If present is defined, check whether at least one of the items in
<a name="586"></a>         *   the list is in the same room, or can be sensed.
<a name="587"></a>         */
<a name="588"></a>        if(propDefined(&amp;present))
<a name="589"></a>        {
<a name="590"></a>            local pList = valToList(present);
<a name="591"></a>            
<a name="592"></a>            /* 
<a name="593"></a>             *   First test for the special case that the present property
<a name="594"></a>             *   specifies a single class. In which case whether anything in the
<a name="595"></a>             *   location of actor matches that class.
<a name="596"></a>             */            
<a name="597"></a>            if(pList.length == 1 &amp;&amp; dataType(pList[1]) == TypeObject &amp;&amp;
<a name="598"></a>               pList[1].isClass())                
<a name="599"></a>            {
<a name="600"></a>                if(actor.getOutermostRoom.allContents.indexWhich(
<a name="601"></a>                    {o: o.ofKind(present) }) == nil)
<a name="602"></a>                    return nil;
<a name="603"></a>            }
<a name="604"></a>            
<a name="605"></a>            /* 
<a name="606"></a>             *   Check whether the first item in list is a property pointer. If
<a name="607"></a>             *   it is we want to use it as a property of the Q object to test
<a name="608"></a>             *   for a sense connection.
<a name="609"></a>             */
<a name="610"></a>            else if(pList.length &gt; 0 &amp;&amp; dataType(pList[1]) == TypeProp)
<a name="611"></a>            {
<a name="612"></a>                /* The first item in the list is a property pointer. */
<a name="613"></a>                local prop = pList[1];
<a name="614"></a>                
<a name="615"></a>                /*  
<a name="616"></a>                 *   Reduce the list to its remaining elements, which should all
<a name="617"></a>                 *   be objects.
<a name="618"></a>                 */
<a name="619"></a>                pList = pList.sublist(2);
<a name="620"></a>                
<a name="621"></a>                /*  
<a name="622"></a>                 *   If no item in pList has a sense path from the actor via the
<a name="623"></a>                 *   prop property, we don't match.
<a name="624"></a>                 */
<a name="625"></a>                if(pList.indexWhich({o: Q.(prop)(actor, o) }) == nil)
<a name="626"></a>                   return nil;
<a name="627"></a>            }
<a name="628"></a>            /* 
<a name="629"></a>             *   Otherwise, simply test for the presence of one of the objects
<a name="630"></a>             *   in the actor's room.
<a name="631"></a>             */
<a name="632"></a>            else
<a name="633"></a>            {
<a name="634"></a>                local loc = actor.getOutermostRoom();
<a name="635"></a>                
<a name="636"></a>                if(pList.indexWhich({o: o.isIn(loc)}) == nil)                    
<a name="637"></a>                    return nil;
<a name="638"></a>            }           
<a name="639"></a>        }
<a name="640"></a>        
<a name="641"></a>        
<a name="642"></a>        /* 
<a name="643"></a>         *   If we haven't failed any of the conditions, we're okay to match, so
<a name="644"></a>         *   return true.
<a name="645"></a>         */
<a name="646"></a>        return true;
<a name="647"></a>    }
<a name="648"></a>    
<a name="649"></a>    /* Add this rule to another rulebook */
<a name="650"></a>    addTo(rb)
<a name="651"></a>    {
<a name="652"></a>        if(rb &amp;&amp; rb.ofKind(RuleBook))
<a name="653"></a>        {
<a name="654"></a>            rb.addToContents(self);
<a name="655"></a>            rulebooks += rb;
<a name="656"></a>        }
<a name="657"></a>    }
<a name="658"></a>    
<a name="659"></a>    /* Remove this rule from a rulebook */    
<a name="660"></a>    removeFrom(rb)
<a name="661"></a>    {
<a name="662"></a>        if(rb &amp;&amp; rb.ofKind(RuleBook))
<a name="663"></a>        {
<a name="664"></a>            rb.removeFromContents(self);
<a name="665"></a>            rulebooks -= rb;
<a name="666"></a>            
<a name="667"></a>            if(location == rb)
<a name="668"></a>                location = nil;
<a name="669"></a>        }
<a name="670"></a>    }
<a name="671"></a>    
<a name="672"></a>    /* 
<a name="673"></a>     *   Move this rule to another rulebook, removing it from all its current
<a name="674"></a>     *   rulebooks. If rb is nil, simply remove this Rule from its current
<a name="675"></a>     *   rulebooks.
<a name="676"></a>     */
<a name="677"></a>     
<a name="678"></a>    moveInto(rb)
<a name="679"></a>    {        
<a name="680"></a>        
<a name="681"></a>        /* 
<a name="682"></a>         *   If rb is actually a RuleBook, add us to its contents and make rb
<a name="683"></a>         *   our current location.
<a name="684"></a>         */
<a name="685"></a>        if(rb &amp;&amp; rb.ofKind(RuleBook))
<a name="686"></a>        {
<a name="687"></a>            rb.addToContents(self);
<a name="688"></a>            
<a name="689"></a>            location = rb;
<a name="690"></a>        }
<a name="691"></a>        
<a name="692"></a>        /* 
<a name="693"></a>         *   Remove us from the contents of all our previous rulebook
<a name="694"></a>         */
<a name="695"></a>        rulebooks.forEach({r: r.removeFromContents(self)});
<a name="696"></a>        
<a name="697"></a>        /* Add our new RuleBooks to our list of rulebooks */
<a name="698"></a>        if(rb)
<a name="699"></a>            rulebooks += rb;
<a name="700"></a>        
<a name="701"></a>        /* Make rb our currently active rulebook */
<a name="702"></a>        rulebook = rb;
<a name="703"></a>        
<a name="704"></a>    }
<a name="705"></a>    
<a name="706"></a>    /*  
<a name="707"></a>     *   The value this rule should return when the stop macro is used at the
<a name="708"></a>     *   end of its follow method. By default we use our rulebook's stopValue.
<a name="709"></a>     */
<a name="710"></a>    stopValue = (rulebook.stopValue)
<a name="711"></a>    
<a name="712"></a>    /* 
<a name="713"></a>     *   The actor to use to compare with the who property of this Rule. This
<a name="714"></a>     *   will normally be gPlayerChar, but the value of this property is taken
<a name="715"></a>     *   from our RuleBook's actor property.
<a name="716"></a>     */
<a name="717"></a>    actor = (rulebook == nil ? gPlayerChar : rulebook.actor)
<a name="718"></a>    
<a name="719"></a>    
<a name="720"></a>    /* ------------------------------------------------------------------- */ 
<a name="721"></a>     /* 
<a name="722"></a>      *   One or more of the following properties, if defined, determine what
<a name="723"></a>      *   conditions this Rule needs to match in order to be executed.
<a name="724"></a>      */
<a name="725"></a>      
<a name="726"></a>    /* 
<a name="727"></a>     *   A Room or Region, or a list of Rooms and/or Regions in which our actor
<a name="728"></a>     *   (usually either gActor or gPlayerChar - the latter by default - must be
<a name="729"></a>     *   for this Rule to match.
<a name="730"></a>     */
<a name="731"></a>    // where = []
<a name="732"></a>    
<a name="733"></a>    /*  
<a name="734"></a>     *   A condition that must hold (or a method returning a Boolean value to
<a name="735"></a>     *   determine whether or not appropriate conditions hold) for this Rule to
<a name="736"></a>     *   match. This is only needed if none of the other properties in this
<a name="737"></a>     *   section provide a way of speficifying the required conditions.
<a name="738"></a>     */
<a name="739"></a>    // when() {}
<a name="740"></a>    
<a name="741"></a>    /* 
<a name="742"></a>     *   An actor, or a list of actors, one of whom must be performing the
<a name="743"></a>     *   current action for this Rule to match.
<a name="744"></a>     */
<a name="745"></a>    // who = []
<a name="746"></a>    
<a name="747"></a>    /*  
<a name="748"></a>     *   A Scene, or a list of Scenes, one of which much be currently happening
<a name="749"></a>     *   for this Rule to match.
<a name="750"></a>     */
<a name="751"></a>    // during = []
<a name="752"></a>    
<a name="753"></a>    /* 
<a name="754"></a>     *   An action, or a list of Actions, one of which (e.g. Take or Jump) must
<a name="755"></a>     *   be the current action in order for this Rule to match.
<a name="756"></a>     */
<a name="757"></a>    // action = []
<a name="758"></a>    
<a name="759"></a>    /* 
<a name="760"></a>     *   An object (or class), or a list of objects (and or classes) one of
<a name="761"></a>     *   which of each the direct, indirect and accessory objects of the current
<a name="762"></a>     *   action must match in order for this Rule to match. (The accessory
<a name="763"></a>     *   object is only relevant if the TIAAction extension is in use).
<a name="764"></a>     */
<a name="765"></a>    // dobj = []
<a name="766"></a>    // iobj = []
<a name="767"></a>    // aobj = []
<a name="768"></a>    
<a name="769"></a>    /* 
<a name="770"></a>     *   An object, class, or other value, or a list of objects and/or classes
<a name="771"></a>     *   or of other values, one of which must match the matchObj property of
<a name="772"></a>     *   our rulebook (which is set by the first parameter of a call to that
<a name="773"></a>     *   RuleBooks's follow() method) for this Rule to match.
<a name="774"></a>     */ 
<a name="775"></a>    // matchObj = []
<a name="776"></a>    
<a name="777"></a>    
<a name="778"></a>    /*  
<a name="779"></a>     *   An object in the presence of which the actor must be for this rule to
<a name="780"></a>     *   match. Presence normally means in the same room, but if this property
<a name="781"></a>     *   is defined as a list and the first item in the list is a property
<a name="782"></a>     *   pointer (&amp;canSee, &amp;canHear, &amp;canReach, &amp;canSmell), this property will
<a name="783"></a>     *   be used to test for tne appropriate sense connection between the actor
<a name="784"></a>     *   and at least one of the other items in the list instead.
<a name="785"></a>     */
<a name="786"></a>    // present = []
<a name="787"></a>;
<a name="788"></a>
<a name="789"></a>#ifdef __DEBUG
<a name="790"></a>modify DebugCtl
<a name="791"></a>   /* list of all debugging options */
<a name="792"></a>    all = inherited + 'rules'
<a name="793"></a>;
<a name="794"></a>#endif
</pre>
<div class=ftr>Adv3Lite Library Reference Manual<br>Generated on 12/09/2014 from adv3Lite version 1.4</div>
</body>
</html>
